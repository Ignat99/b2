AS65 Assembler for R6502 [1.42].  Copyright 1994-2007, Frank A. Kingswood                                                Page    1
----------------------------------------------------- 6502_decimal_test.a65 ------------------------------------------------------

356 lines read, no errors in pass 1.
                        ; Verify decimal mode behavior
                        ; Written by Bruce Clark.  This code is public domain.
                        ; see http://www.6502.org/tutorials/decimal_mode.html
                        ;
                        ; Returns:
                        ;   ERROR = 0 if the test passed
                        ;   ERROR = 1 if the test failed
                        ;   modify the code at the DONE label for desired program end
                        ;
                        ; This routine requires 17 bytes of RAM -- 1 byte each for:
                        ;   AR, CF, DA, DNVZC, ERROR, HA, HNVZC, N1, N1H, N1L, N2, N2L, NF, VF, and ZF
                        ; and 2 bytes for N2H
                        ;
                        ; Variables:
                        ;   N1 and N2 are the two numbers to be added or subtracted
                        ;   N1H, N1L, N2H, and N2L are the upper 4 bits and lower 4 bits of N1 and N2
                        ;   DA and DNVZC are the actual accumulator and flag results in decimal mode
                        ;   HA and HNVZC are the accumulator and flag results when N1 and N2 are
                        ;     added or subtracted using binary arithmetic
                        ;   AR, NF, VF, ZF, and CF are the predicted decimal mode accumulator and
                        ;     flag results, calculated using binary arithmetic
                        ;
                        ; This program takes approximately 1 minute at 1 MHz (a few seconds more on
                        ; a 65C02 than a 6502 or 65816)
                        ;
                        
                        ; Configuration:
                        ; cputype = 1         ; 0 = 6502, 1 = 65C02, 2 = 65C816
0000 =                  vld_bcd = 0         ; 0 = allow invalid bcd, 1 = valid bcd only
0001 =                  chk_a   = 1         ; check accumulator
0001 =                  chk_n   = 1         ; check sign (negative) flag
0001 =                  chk_v   = 1         ; check overflow flag
0001 =                  chk_z   = 1         ; check zero flag
0001 =                  chk_c   = 1         ; check carry flag
                        
                        end_of_test macro
                        	    sta $ff00
                                        db  $db     ;execute 65C02 stop instruction
                                    endm
                        
                                bss
0000 =                          org 0
                        ; operands - register Y = carry in
0000 =                  N1      ds  1
0001 =                  N2      ds  1
                        ; binary result
0002 =                  HA      ds  1
0003 =                  HNVZC   ds  1
                                            ;04
                        ; decimal result
0004 =                  DA      ds  1
0005 =                  DNVZC   ds  1
                        ; predicted results
0006 =                  AR      ds  1
0007 =                  NF      ds  1
                                            ;08
0008 =                  VF      ds  1
0009 =                  ZF      ds  1
000a =                  CF      ds  1
000b =                  ERROR   ds  1
                                            ;0C
                        ; workspace
000c =                  N1L     ds  1
000d =                  N1H     ds  1
000e =                  N2L     ds  1
000f =                  N2H     ds  2
                        
                                code
0200 =                          org $200
0200 : a001             TEST    ldy #1    ; initialize Y (used to loop through carry flag values)
0202 : 840b                     sty ERROR ; store 1 in ERROR until the test passes
0204 : a900                     lda #0    ; initialize N1 and N2
0206 : 8500                     sta N1
0208 : 8501                     sta N2
020a : a501             LOOP1   lda N2    ; N2L = N2 & $0F
020c : 290f                     and #$0F  ; [1] see text
                                if  vld_bcd = 1
                                    cmp #$0a
                                    bcs NEXT2
                                endif
020e : 850e                     sta N2L
0210 : a501                     lda N2    ; N2H = N2 & $F0
0212 : 29f0                     and #$F0  ; [2] see text
                                if  vld_bcd = 1
                                    cmp #$a0
                                    bcs NEXT2
                                endif
0214 : 850f                     sta N2H
0216 : 090f                     ora #$0F  ; N2H+1 = (N2 & $F0) + $0F
0218 : 8510                     sta N2H+1
021a : a500             LOOP2   lda N1    ; N1L = N1 & $0F
021c : 290f                     and #$0F  ; [3] see text
                                if  vld_bcd = 1
                                    cmp #$0a
                                    bcs NEXT1
                                endif
021e : 850c                     sta N1L
0220 : a500                     lda N1    ; N1H = N1 & $F0
0222 : 29f0                     and #$F0  ; [4] see text
                                if  vld_bcd = 1
                                    cmp #$a0
                                    bcs NEXT1
                                endif
0224 : 850d                     sta N1H
0226 : 204f02                   jsr ADD
0229 : 20f202                   jsr A6502
022c : 20cd02                   jsr COMPARE
022f : d01a                     bne DONE
0231 : 209302                   jsr SUB
0234 : 20fb02                   jsr S6502
0237 : 20cd02                   jsr COMPARE
023a : d00f                     bne DONE
023c : e600             NEXT1   inc N1    ; [5] see text
023e : d0da                     bne LOOP2 ; loop through all 256 values of N1
0240 : e601             NEXT2   inc N2    ; [6] see text
0242 : d0c6                     bne LOOP1 ; loop through all 256 values of N2
0244 : 88                       dey
0245 : 10c3                     bpl LOOP1 ; loop through both values of the carry flag
0247 : a900                     lda #0    ; test passed, so store 0 in ERROR
0249 : 850b                     sta ERROR
024b :                  DONE    
                                end_of_test
024b : 8d00ff          >	    sta $ff00
024e : db              >                db  $db     ;execute 65C02 stop instruction
                        
                                   
                        ; Calculate the actual decimal mode accumulator and flags, the accumulator
                        ; and flag results when N1 is added to N2 using binary arithmetic, the
                        ; predicted accumulator result, the predicted carry flag, and the predicted
                        ; V flag   
                        ;          
024f : f8               ADD     sed       ; decimal mode
0250 : c001                     cpy #1    ; set carry if Y = 1, clear carry if Y = 0
0252 : a500                     lda N1
0254 : 6501                     adc N2
0256 : 8504                     sta DA    ; actual accumulator result in decimal mode
0258 : 08                       php
0259 : 68                       pla
025a : 8505                     sta DNVZC ; actual flags result in decimal mode
025c : d8                       cld       ; binary mode
025d : c001                     cpy #1    ; set carry if Y = 1, clear carry if Y = 0
025f : a500                     lda N1
0261 : 6501                     adc N2
0263 : 8502                     sta HA    ; accumulator result of N1+N2 using binary arithmetic
                                   
0265 : 08                       php
0266 : 68                       pla
0267 : 8503                     sta HNVZC ; flags result of N1+N2 using binary arithmetic
0269 : c001                     cpy #1
026b : a50c                     lda N1L
026d : 650e                     adc N2L
026f : c90a                     cmp #$0A
0271 : a200                     ldx #0
0273 : 9006                     bcc A1
0275 : e8                       inx
0276 : 6905                     adc #5    ; add 6 (carry is set)
0278 : 290f                     and #$0F
027a : 38                       sec
027b : 050d             A1      ora N1H
                        ;          
                        ; if N1L + N2L <  $0A, then add N2 & $F0
                        ; if N1L + N2L >= $0A, then add (N2 & $F0) + $0F + 1 (carry is set)
                        ;          
027d : 750f                     adc N2H,x
027f : 08                       php
0280 : b004                     bcs A2
0282 : c9a0                     cmp #$A0
0284 : 9003                     bcc A3
0286 : 695f             A2      adc #$5F  ; add $60 (carry is set)
0288 : 38                       sec
0289 : 8506             A3      sta AR    ; predicted accumulator result
028b : 08                       php
028c : 68                       pla
028d : 850a                     sta CF    ; predicted carry result
028f : 68                       pla
                        ;          
                        ; note that all 8 bits of the P register are stored in VF
                        ;          
0290 : 8508                     sta VF    ; predicted V flags
0292 : 60                       rts
                                   
                        ; Calculate the actual decimal mode accumulator and flags, and the
                        ; accumulator and flag results when N2 is subtracted from N1 using binary
                        ; arithmetic
                        ;          
0293 : f8               SUB     sed       ; decimal mode
0294 : c001                     cpy #1    ; set carry if Y = 1, clear carry if Y = 0
0296 : a500                     lda N1
0298 : e501                     sbc N2
029a : 8504                     sta DA    ; actual accumulator result in decimal mode
029c : 08                       php
029d : 68                       pla
029e : 8505                     sta DNVZC ; actual flags result in decimal mode
02a0 : d8                       cld       ; binary mode
02a1 : c001                     cpy #1    ; set carry if Y = 1, clear carry if Y = 0
02a3 : a500                     lda N1
02a5 : e501                     sbc N2
02a7 : 8502                     sta HA    ; accumulator result of N1-N2 using binary arithmetic
                                   
02a9 : 08                       php
02aa : 68                       pla
02ab : 8503                     sta HNVZC ; flags result of N1-N2 using binary arithmetic
02ad : 60                       rts
                                   
                                if cputype != 1
                        ; Calculate the predicted SBC accumulator result for the 6502 and 65816
                        ;          
                        SUB1        cpy #1    ; set carry if Y = 1, clear carry if Y = 0
                                    lda N1L
                                    sbc N2L
                                    ldx #0
                                    bcs S11
                                    inx
                                    sbc #5    ; subtract 6 (carry is clear)
                                    and #$0F
                                    clc
                        S11         ora N1H
                        ;          
                        ; if N1L - N2L >= 0, then subtract N2 & $F0
                        ; if N1L - N2L <  0, then subtract (N2 & $F0) + $0F + 1 (carry is clear)
                        ;          
                                    sbc N2H,x
                                    bcs S12
                                    sbc #$5F  ; subtract $60 (carry is clear)
                        S12         sta AR
                                    rts
                                endif
                                   
                                if cputype = 1
                        ; Calculate the predicted SBC accumulator result for the 6502 and 65C02
                        ;
02ae : c001             SUB2        cpy #1    ; set carry if Y = 1, clear carry if Y = 0
02b0 : a50c                         lda N1L
02b2 : e50e                         sbc N2L
02b4 : a200                         ldx #0
02b6 : b004                         bcs S21
02b8 : e8                           inx
02b9 : 290f                         and #$0F
02bb : 18                           clc
02bc : 050d             S21         ora N1H
                        ;          
                        ; if N1L - N2L >= 0, then subtract N2 & $F0
                        ; if N1L - N2L <  0, then subtract (N2 & $F0) + $0F + 1 (carry is clear)
                        ;          
02be : f50f                         sbc N2H,x
02c0 : b002                         bcs S22
02c2 : e95f                         sbc #$5F   ; subtract $60 (carry is clear)
02c4 : e000             S22         cpx #0
02c6 : f002                         beq S23
02c8 : e906                         sbc #6
02ca : 8506             S23         sta AR     ; predicted accumulator result
02cc : 60                           rts
                                endif
                                   
                        ; Compare accumulator actual results to predicted results
                        ;          
                        ; Return:  
                        ;   Z flag = 1 (BEQ branch) if same
                        ;   Z flag = 0 (BNE branch) if different
                        ;          
02cd :                  COMPARE 
                                if chk_a = 1
02cd : a504                         lda DA
02cf : c506                         cmp AR
02d1 : d01e                         bne C1
                                endif
                                if chk_n = 1
02d3 : a505                         lda DNVZC ; [7] see text
02d5 : 4507                         eor NF
02d7 : 2980                         and #$80  ; mask off N flag
02d9 : d016                         bne C1
                                endif
                                if chk_v = 1
02db : a505                         lda DNVZC ; [8] see text
02dd : 4508                         eor VF
02df : 2940                         and #$40  ; mask off V flag
02e1 : d00e                         bne C1    ; [9] see text
                                endif
                                if chk_z = 1
02e3 : a505                         lda DNVZC
02e5 : 4509                         eor ZF    ; mask off Z flag
02e7 : 2902                         and #2
02e9 : d006                         bne C1    ; [10] see text
                                endif
                                if chk_c = 1
02eb : a505                         lda DNVZC
02ed : 450a                         eor CF
02ef : 2901                         and #1    ; mask off C flag
                                endif
02f1 : 60               C1      rts
                                   
                        ; These routines store the predicted values for ADC and SBC for the 6502,
                        ; 65C02, and 65816 in AR, CF, NF, VF, and ZF
                        
                                if cputype = 0
                        
                        A6502       lda VF      ; 6502
                        ;          
                        ; since all 8 bits of the P register were stored in VF, bit 7 of VF contains
                        ; the N flag for NF
                        ;          
                                    sta NF
                                    lda HNVZC
                                    sta ZF
                                    rts
                                   
                        S6502       jsr SUB1
                                    lda HNVZC
                                    sta NF
                                    sta VF
                                    sta ZF
                                    sta CF
                                    rts
                        
                                endif
                                if  cputype = 1
                        
02f2 : a506             A6502       lda AR      ; 65C02
02f4 : 08                           php
02f5 : 68                           pla
02f6 : 8507                         sta NF
02f8 : 8509                         sta ZF
02fa : 60                           rts
                                   
02fb : 20ae02           S6502       jsr SUB2
02fe : a506                         lda AR
0300 : 08                           php
0301 : 68                           pla
0302 : 8507                         sta NF
0304 : 8509                         sta ZF
0306 : a503                         lda HNVZC
0308 : 8508                         sta VF
030a : 850a                         sta CF
030c : 60                           rts
                        
                                endif
                                if  cputype = 2   
                        
                        A6502       lda AR      ; 65C816
                                    php
                                    pla
                                    sta NF
                                    sta ZF
                                    rts
                                   
                        S6502       jsr SUB1
                                    lda AR
                                    php
                                    pla
                                    sta NF
                                    sta ZF
                                    lda HNVZC
                                    sta VF
                                    sta CF
                                    rts
                        
                                endif
                        
02fb =                          end TEST
                        
No errors in pass 2.
Wrote binary from address $0200 through $030c.
Total size 269 bytes.
Program start address is at $0200 (512).
